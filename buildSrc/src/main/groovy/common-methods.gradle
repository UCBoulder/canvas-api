// Exports a common set of methods to one place to reduce code duplication
// References:  https://stackoverflow.com/a/18718802/1079354
// and https://stackoverflow.com/a/23290820/1079354

def retrieveUsername() {
    return project.hasProperty('artifactoryUsername') ? project.property('artifactoryUsername') : ''
}

def retrievePassword() {
    return project.hasProperty('artifactoryPassword') ? project.property('artifactoryPassword') : ''
}

def isReleaseBuild() {
    return project.hasProperty("release") && Boolean.valueOf((String) project.property("release"))
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

// Only deploy production artifacts to libs-release-local.
// No property - assume running in development, and deploy to snapshots.
def retrieveRepository() {
    def releaseRepoUrl = "https://artifactory.colorado.edu/artifactory/software-engineering-libs-release-local"
    def snapshotRepoUrl = "https://artifactory.colorado.edu/artifactory/software-engineering-libs-snapshot-local"

    if (isReleaseBuild()) {
        return releaseRepoUrl
    } else {
        return snapshotRepoUrl
    }
}

ext {
    retrieveUsername = this.&retrieveUsername
    retrievePassword = this.&retrievePassword
    isReleaseBuild = this.&isReleaseBuild
    retrieveRepository = this.&retrieveRepository
    getCheckedOutGitCommitHash = this.&getCheckedOutGitCommitHash
}